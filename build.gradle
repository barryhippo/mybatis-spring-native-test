plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.graalvm.buildtools.native' version '0.9.18'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        maven {
            name 'aliyun'
            url 'https://maven.aliyun.com/repository/public'
        }
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        dependencies {
            dependency('org.springframework.boot:spring-boot-starter:3.0.2') {
                exclude('org.apache.logging.log4j:log4j-to-slf4j')
                exclude('org.slf4j:jul-to-slf4j')
            }

            dependency('org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.1') {
                exclude('org.springframework.boot:spring-boot-starter-jdbc')
                exclude('org.springframework.boot:spring-boot-starter')
            }

            dependency('com.baomidou:mybatis-plus-boot-starter:3.5.3.1')
            dependency('org.reflections:reflections:0.10.2')
        }
    }

    dependencies {
        implementation('ch.qos.logback:logback-classic')
        compileOnly('org.projectlombok:lombok')
        annotationProcessor('org.projectlombok:lombok')
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-starter-jdbc')

    implementation('org.mybatis.spring.boot:mybatis-spring-boot-starter')

    runtimeOnly('com.mysql:mysql-connector-j')
    implementation('org.javassist:javassist:3.29.2-GA')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('io.projectreactor:reactor-test')

}

tasks.named('test') {
    useJUnitPlatform()
}
